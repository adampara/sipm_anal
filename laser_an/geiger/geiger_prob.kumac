macro geiger_prob dir=../exp/Ham-025U-4_Oct_19 it=1 iv=1  coh=1 deb=0 f_dir=/det_rd_results/sipm_anal/laser_an/f_util il=4975 iu=5025


if [deb] = 1 then
	mess geiger_prob: dir = [dir] it=[it] iv =[iv]  coherent noise flag = [coh]
endif

exec ../setup
kuip/set/filecase keep

vector/read t123 [dir]/temps.dat
t = t123([it])
vector/del t123

vec/read v123 [dir]/[t]/bias_volt.dat
v = v123([iv])
v = $FORMAT([v],F5.2)
vector/del v123

vec/read yyinc [dir]/[t]/[v]_yinc.dat
ystep = yyinc(1)
mess ysetp = [ystep]

vec/read sig_noise [dir]/[t]_noise/baseline_sigma_coh_1.dat
cut_noise = 3.*sig_noise([iv])

vec/read one_pe [dir]/[t]_breakdown/[v]_1pe.dat

file_t = sweeps_nt
exec open_nt dir=[dir] it=[it] iv=[iv] file_t=[file_t]

*c   get the coherent noise vector
exec get_coh_noise v=[v] coh=[coh] temp_dir=[dir]/[t]

*     load the function explicitely, nt/loop does not allow absolute adressing
 funp = [f_dir]/peak_pos.f
call [funp](1,100)

funv = [f_dir]/peak_val.f
call [funv](1,100)

nbin=200

exec vect_cre vecname=cont len=[nbin]
exec vect_cre vecname=xpos len=[nbin]
exec vect_cre vecname=fun len=[nbin]
exec vect_cre vecname=zero len=1
exec vect_cre vecname=ibreak len=1

peak = one_pe(1)/[ystep]


npeak = $SIGMA(int([peak]+0.5))
wpeak = $SIGMA(int([npeak]*0.2+0.5))
nnoise = $SIGMA(int([cut_noise]/[ystep]+0.5))
mess one pe peak us at bin [npeak], width = [wpeak] , 3sigma noise = [nnoise]

f_gen = /det_rd_results/sipm_anal/fort_gen
exec vect_cre vecname=par len=3

i1 = [npeak] - [wpeak]+1
i2 = [npeak] + [wpeak]+1

xup = ([nbin]-0.5)*[ystep]
xl = -0.5*[ystep]
title = peaks T=[t], V=[v]

hist/cre/1d 102 [title] [nbin] 0. [xup] 
nt/proj 102 30.peak_val([il],[iu])

hist/get/cont 102 cont
hist/get/abscissa 102 xpos

*     fit the one pe peak
h/fit 102([i1]:[i2]) g
call [f_gen]/fitpar.f
call zero_prob.f([nnoise])
vec/copy fun fun_peak
icut = ibreak(1)
mess icut = [icut]

*     fit the noise peak
h/fit 102(1:[nnoise]) g
call [f_gen]/fitpar.f
call zero_prob.f([nnoise])



call pzero.f([icut])
nzero = zero(1)
mess signal region nzero = [nzero]
***   random gate 



ngate = [iu] - [il]

ilr = 500
iur = [ilr] + [ngate]
hist/cre/1d 101 [title] [nbin] 0. [xup] 
nt/proj 101 30.peak_val([ilr],[iur])
hist/get/cont 101 cont
hist/get/abscissa 101 xpos

*     fit the one pe peak
h/fit 101([i1]:[i2]) g
call [f_gen]/fitpar.f
call zero_prob.f([nnoise])

*     fit the noise peak
h/fit 101(1:[nnoise]) g
call [f_gen]/fitpar.f
call zero_prob.f([nnoise])

call pzero.f([icut])
nzeror = zero(1)
mess background  region nzero = [nzeror]

tot = $HINFO(101,'ENTRIES')

pzero = [nzero]/[tot]
pzeror = [nzeror]/[tot]

pone = 1.- [pzero]
poner = 1. - [pzeror]

pcor = ([pone] - [poner])/(1.-[poner])
pzcor = 1. - [pcor]
 mess corrected zero probability = [pzcor] 
sigma pmv = -LOG([pzero])

sigma pmerr = 1./SQRT([nzero])


if [deb]=0 then
  file=[dir]/[t]_geiger/[v]_nphot_il_[il]_iu_[iu].dat
  vec/write pmv,pmerr [file]
  file_hist = [dir]/[t]_geiger/[v]_nphot_il_[il]_iu_[iu].hist
  exec save_hist idh=0 hist_file=[file_hist]
endif